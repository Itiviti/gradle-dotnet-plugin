import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id 'com.gradle.plugin-publish' version '1.3.0'
    id 'org.jetbrains.kotlin.jvm' version '1.9.10'
    id 'groovy'
    id 'maven-publish'
    id 'jacoco'
}

repositories {
    mavenCentral()
    maven {
        url = uri("https://plugins.gradle.org/m2/")
    }
}

jacoco.toolVersion = '0.8.10'
sourceCompatibility = JavaVersion.VERSION_1_8
kotlin {
    compilerOptions {
        jvmTarget.set(JvmTarget.JVM_1_8)
    }
}

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'org.reflections:reflections:0.9.12'
    implementation 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:4.0.0.2929'

    testImplementation 'org.spockframework:spock-core:2.0-groovy-2.5'
}

test {
    useJUnitPlatform()
}

gradlePlugin {
    website = 'https://www.github.com/Itiviti/gradle-dotnet-plugin'
    vcsUrl = 'https://www.github.com/Itiviti/gradle-dotnet-plugin'
    description = 'gradle-dotnet-plugin is a Gradle plugin for .net sdk project build'
    plugins {
        dotnetPlugin {
            id = 'com.itiviti.dotnet'
            implementationClass = 'com.itiviti.DotnetPlugin'
            displayName = 'Gradle Dotnet plugin'
            description = 'Plugin for building .net sdk project'
            tags.set(['dotnet', '.net core', 'msbuild', 'nunit', 'nuget'])
        }

        sonarPlugin {
            id = 'com.itiviti.dotnet.sonar'
            implementationClass = 'com.itiviti.DotnetSonarPlugin'
            displayName = 'Gradle Dotnet Sonar plugin'
            description = 'Plugin for wrapping .net sdk project build with sonar'
            tags.set(['dotnet', '.net core', 'msbuild', 'sonarqube'])
        }
    }
}

println "##vso[task.setvariable variable=project.version]${project.version}"
